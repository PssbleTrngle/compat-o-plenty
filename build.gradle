buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath(group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true)
        classpath 'org.parchmentmc:librarian:1.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'org.parchmentmc.librarian.forgegradle'


version = project.version
group = 'com.seleneandmana.compatoplenty'
archivesBaseName = 'Compat-O-Plenty'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'parchment', version: '2023.09.03-1.20.1'

    runs {
        client {}
        server {}
        data {
            args '--mod', 'compatoplenty', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'), '--existing-mod', 'biomesoplenty', '--existing-mod', 'quark', '--existing-mod', 'blueprint'
        }

        forEach {
            it.workingDirectory project.file('run')
            it.property 'forge.logging.markers', 'REGISTRIES'
            it.property 'forge.logging.console.level', 'debug'
            it.property 'mixin.env.remapRefMap', 'true'
            it.property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            it.mods {
                compatoplenty {
                    source sourceSets.main
                }
            }
        }
    }
}


sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.1.3'
    implementation fg.deobf("com.teamabnormals:blueprint:1.20.1-7.1.0")
    implementation fg.deobf("curse.maven:bop-220318:4764804")
    implementation fg.deobf("curse.maven:farmersdelight-398521:5051242")
    implementation fg.deobf("com.teamabnormals:boatload:1.20.1-5.0.1")
    implementation fg.deobf("curse.maven:woodworks-543610:5292418")
    implementation fg.deobf("curse.maven:clayworks-592162:5302012")
    compileOnly fg.deobf("curse.maven:vertical-slabs-compat-724387:5022329")
    implementation fg.deobf("curse.maven:selene-499980:5478857")
    implementation fg.deobf("curse.maven:quark-243121:5418252")
    implementation fg.deobf("curse.maven:zeta-968868:5418213")
    implementation fg.deobf("curse.maven:twigs-496913:4998195")

    runtimeOnly fg.deobf("curse.maven:terrablender-563928:5378180")

    runtimeOnly fg.deobf("curse.maven:jei-238222:5419575")
}

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url "https://maven.jaackson.me"
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property("version", version)

    filesMatching('META-INF/mods.toml') {
        expand(
                version: version,
        )
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Compat O' Plenty",
                "Specification-Vendor"    : "Selene and Mana",
                "Specification-Version"   : "1",
                "Implementation-Title"    : "Compat O' Plenty",
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "Selene and Mana",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
